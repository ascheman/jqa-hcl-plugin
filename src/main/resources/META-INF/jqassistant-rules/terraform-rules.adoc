[[terraform:Hcl2Terraform]]
.Tag all objects defined in .tf or .tfvars files als Terraform
[source,cypher,role=concept]
----
MATCH
  (file:HCL:File) -[*]-> (h:HCL)
WHERE
  file.fileName =~ '.+\\.tf(|vars)$'
SET
  file:Terraform,
  h:Terraform
RETURN file

UNION
MATCH (file:HCL:File) -[*]-> (t:Terraform)
WHERE t.identifier = 'data'
SET t:Data
RETURN file

UNION
MATCH (file:HCL:File) -[*]-> (t:Terraform)
WHERE t.identifier = 'locals'
SET t:Local
RETURN file

UNION
MATCH (file:HCL:File) -[*]-> (t:Terraform)
WHERE t.identifier = 'output'
SET t:Output
RETURN file

UNION
MATCH (file:HCL:File) -[*]-> (t:Terraform)
WHERE t.identifier = 'resource'
SET t:Resource
RETURN file

UNION
MATCH (file:HCL:File) -[*]-> (t:Terraform)
WHERE t.identifier = 'variable'
SET t:Variable
RETURN file
----

[[terraform:ResourceTypes]]
.Define the types for all Terraform Resources
[source,cypher,role=concept,requiresConcepts=terraform:Hcl2Terraform]
----
MATCH (t:Terraform)-[:HAS_BLOCK]->(r:Terraform:Resource)
SET r.type = t.name
WITH DISTINCT t.name as type_name
CREATE (c:Terraform:ResourceType {name: type_name})
WITH c
MATCH (r:Terraform:Resource{type:c.name})
MERGE (c)-[:HAS_OBJECT]->(r)
RETURN *
----

[[terraform:File2Resource]]
.Attach defined resources directly to the respective file
[source,cypher,role=concept,requiresConcepts=terraform:Hcl2Terraform]
----
MATCH (f:Terraform:File)-[:HAS_BLOCK]->(:Terraform {name:"resource"})-[:HAS_BLOCK]->()-[:HAS_BLOCK]->(r:Terraform:Resource)
MERGE (f)-[:DEFINES]->(r)
RETURN *
----

[[terraform:ResourceUsageRelations]]
.Create "USES" relations between Terraform Resources
[source,cypher,role=concept,requiresConcepts=terraform:ResourceTypes]
----
MATCH (src:Terraform:Resource)-[*]->()-[:REFERS_TO]->
  (v:Terraform:Variable {identifier:"id"})
  <-[:HAS_ATTRIBUTE]-(tgt:Terraform:Resource)
MERGE (src)-[:USES]->(tgt)
RETURN *
----

//[[my-rules:MyConstraint]]
//.A human readable description of the constraint.
//[source,cypher,role=constraint,requiresConcepts="my-rules:MyConcept",severity=blocker]
//----
// //MATCH //// ...
// //WHERE //// ...
// //RETURN //// ...
//----

[[terraform:TerraformDefaultGroup]]
.Include all concepts to extend HCL objects to Terraform if necessary
[role=group,includeConcepts="terraform:*"]
== Terraform Default Group